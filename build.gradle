plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.2.0'
    id "com.diffplug.spotless" version "6.20.0" // formatting for Java 17
}

repositories {
    mavenCentral()
}

spotless {
    java {
        googleJavaFormat('1.17.0')
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'com.exclamationlabs.connid'

def build_version = System.getenv('BUILD_NUMBER') == null ? System.currentTimeMillis() : System.getenv('BUILD_NUMBER')
project.version="${software_version}-${build_version}"

dependencies {
    api 'net.tirasa.connid:connector-test-common:1.5.0.0'
    api 'org.apache.httpcomponents:httpclient:4.5.14'

    api 'org.mockito:mockito-junit-jupiter:4.11.0'

    api 'org.apache.commons:commons-lang3:3.13.0'
    api 'net.tirasa.connid:connector-framework-internal:1.5.0.0'

    api 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'source'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task copyPom(type: Copy) {
    from 'build/publications/connector_base/pom-default.xml'
    into 'build/libs'
    rename ('pom-default.xml', "${project.name}-${project.version}.pom")
}

publishing {
    publications {
        connector_base(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = "Connector Base: Test Support"
                description = "Test Support Dependency for Base Connector Framework"
                url = "https://github.com/ExclamationLabs/connector-base-test-support"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    url = "https://github.com/ExclamationLabs/connector-base-test-support/tree/master"
                }
            }
        }
    }

    repositories {
        maven {
            url "artifactregistry://us-east1-maven.pkg.dev/provision-general-operations/provision-public/"
            credentials {
                username = "${System.getenv('ARTIFACT_REGISTRY_USER')}"
                password = "${System.getenv('ARTIFACT_REGISTRY_KEY')}"
            }
        }
    }
}

test {
    useJUnitPlatform() {}
}

build.dependsOn(copyPom)
copyPom.dependsOn(publishToMavenLocal)


